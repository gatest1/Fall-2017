global proc ControlWindow() {
	//builds the window UI
	
	//assign object name for window UI object
	string $mWindow = "ctrlWindow";
	
	//delete the window if it has already been created
	if(`window -exists $mWindow` == true)
		deleteUI -window $mWindow;

	//create the main window and and column for controls
	$mWindow = `window -title "Control Creator" -sizeable 0 $mWindow`;
	string $mColumn = `columnLayout -adjustableColumn 1 -columnAttach "both" 2 -parent $mWindow`;
	
	//create buttons and fields
	string $circleBtn = `button -parent $mColumn -label "Circle"`;
	string $squareBtn = `button -parent $mColumn -label "Square"`;
	string $cubeBtn = `button -parent $mColumn -label "Cube"`;
	string $txtField = `textFieldGrp -parent $mColumn -label "Control Name" -placeholderText "Enter name"`;
	string $floatField = `floatSliderGrp -parent $mColumn -label "Size" -minValue 0 -maxValue 10 -field 1 -value 1`;
	
	//use the edit flag on each button to add the commands. could not assign previously when built
	//because the text and float field groups had not yet been created.
	button -e -command ("CreateControl(\"circle\", \"" + $txtField + "\", \"" + $floatField + "\")") $circleBtn;
	button -e -command ("CreateControl(\"square\", \"" + $txtField + "\", \"" + $floatField + "\")") $squareBtn;
	button -e -command ("CreateControl(\"cube\", \"" + $txtField + "\", \"" + $floatField + "\")") $cubeBtn;
	
	window -e -width 400 -height 125 $mWindow;
	showWindow($mWindow);
}


global proc string CreateControl(string $type, string $nameField, string $scaleField) {
	//creates a control of specified type
	
	//query controls to get field values and assign to a variable
	float $scale = `floatSliderGrp -q -value $scaleField`;
	string $name = `textFieldGrp -q -text $nameField`;
	
	//generate generic name if none is provided in text field
	if($name == "")
		$name = "Generic";
		
	$name = $name + "_Ctrl";
	
	//create an additional variable for the control
	string $ctrl;
	
	//create specified control type
	if ($type == "circle") {
		string $tCtrl[] = `circle -c 0 0 0 -nr 0 1 0 -sw 360 -r 1 -d 3 -ut 0 -tol 0.01 -s 8 -ch 1`;
		$ctrl = $tCtrl[0];
	}
	else if($type == "square") {
		$ctrl = `curve -d 1 -p 1 0 -1 -p -1 0 -1 -p -1 0 1 -p 1 0 1 -p 1 0 -1 -k 0 -k 1 -k 2 -k 3 -k 4`;
	}
	else if($type == "cube") {
		$ctrl = `curve -d 1 -p 0.5 0.5 0.5 -p -0.5 0.5 0.5 -p -0.5 -0.5 0.5 -p 0.5 -0.5 0.5 -p 0.5 0.5 0.5 -p 0.5 0.5 -0.5 -p 0.5 -0.5 -0.5 -p 0.5 -0.5 0.5 -p 0.5 -0.5 -0.5 -p -0.5 -0.5 -0.5 -p -0.5 -0.5 0.5 -p -0.5 0.5 0.5 -p -0.5 0.5 -0.5 -p -0.5 -0.5 -0.5 -p -0.5 0.5 -0.5 -p 0.5 0.5 -0.5 -k 0 -k 1 -k 2 -k 3 -k 4 -k 5 -k 6 -k 7 -k 8 -k 9 -k 10 -k 11 -k 12 -k 13 -k 14 -k 15` ;
	}
	
	//scale the object globally using the float field value
	scale -r $scale $scale $scale $ctrl;
	
	//rename control and delete history
	$ctrl = `rename $ctrl $name`;
	delete -ch $ctrl;
	
	//return control name
	return $ctrl;
}